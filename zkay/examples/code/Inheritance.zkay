pragma zkay ^0.2.0;

contract Inheritance {

	final uint DAY = 24 * 60 * 60;
	final uint timeUntilAssumedDead = 120 * DAY;

	mapping(address!x => uint32@x<+>) balance;
	mapping(address!x => uint32@x<+>) total_inheritance_pledged;
	mapping(address => mapping(address!x => uint32@x)) inheritance_pledged_recv;
	mapping(address!x => mapping(address => uint32@x)) inheritance_pledged_send;
	mapping(address => bool) registered;
	mapping(address => uint) last_seen;

	uint _now = now; // For testing purposes only, need to be able to advance time

	function register() public {
		require(!registered[me]);
		balance[me] = 0;
		registered[me] = true;
		keep_alive();
	}

	function buy() public payable {
		require(registered[me]);
		balance[me] += uint32(msg.value);
		keep_alive();
	}

	function send_tokens(uint32@me v, address receiver) public {
		require(registered[me] && registered[receiver]);
		require(reveal(balance[me] - total_inheritance_pledged[me] >= v, all));

		balance[me] -= v;
		balance[receiver] += reveal(v, receiver);
		keep_alive();
	}

	function pledge_inheritance(address recipient, uint32@me amount) public {
		uint32@me old_amount = inheritance_pledged_send[me][recipient];
		require(reveal(balance[me] - total_inheritance_pledged[me] + old_amount >= amount, all));

		inheritance_pledged_send[me][recipient] = amount;
		inheritance_pledged_recv[me][recipient] = reveal(amount, recipient);
		total_inheritance_pledged[me] += amount - old_amount;
		keep_alive();
	}

	function claim_inheritance(address sender) public {
		require(_now - last_seen[sender] >= timeUntilAssumedDead);

		uint32@me amount = inheritance_pledged_recv[sender][me];
		balance[me] += amount;
		balance[sender] -= reveal(amount, sender);
		total_inheritance_pledged[sender] -= reveal(amount, sender);
		inheritance_pledged_recv[sender][me] = 0;
		inheritance_pledged_send[sender][me] = 0;
		keep_alive();
	}

	function keep_alive() public {
		last_seen[me] = _now;
	}

	function _test_advance_time() public {
		_now += timeUntilAssumedDead;
	}
}
